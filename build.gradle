apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin:  'idea'
//plugins {
//    id "java"
//  id "eclipse"
//    id "idea"
//    id "maven"
//}

group = 'org.asdfgamer.utils'
version = '0.9'
ext.moduleName= 'org.asdfgamer.utils'
mainClassName='org.asdfgamer.utils.config.tests.Main'

description = "Dies sind verschiedene Utility-Klassen die ich brauche."

sourceCompatibility = 1.9
targetCompatibility = 1.9

repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.6'
    compile group: 'commons-net', name: 'commons-net', version:'3.6'

    // JUnit and Mockito
    testCompile "junit:junit:4.12"
    //Da es ein Problem mit dem Laden der Ressourcen f√ºr die Tests gibt (Die RessourceBundels werden nicht gefunden)
    compile "junit:junit:4.12"
    //test
}

//Changes for Jigsaw


compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath
        ]
        classpath = files()
    }
}
//End of Changes for Jigsaw